apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 237
        versionName '2.5.17'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "resourcePackageName": "org.transdroid",
                        "androidManifestFile": file("src/main/AndroidManifest.xml").absolutePath
                ]
            }
        }
    }
    signingConfigs {
        releaseConfig {
            def propsFile = rootProject.file('keystore.properties')
            def configName = 'releaseConfig'

            if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                keyAlias = props['keyAlias']
                storeFile = file(props['storeFile'])
                keyPassword = props['keyPassword']
                storePassword = props['storePassword']
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        full {
            dimension "version"
            applicationId 'org.transdroid.full'
            resValue "string", "search_history_authority", applicationId + ".search.SearchHistoryProvider"
        }
        lite {
            dimension "version"
            applicationId 'org.transdroid.lite'
            resValue "string", "search_history_authority", applicationId + ".search.SearchHistoryProvider"
        }
    }
    lintOptions {
        disable 'MissingTranslation', 'ExtraTranslation', 'StringFormatInvalid', 'ValidFragment'
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation 'org.androidannotations:androidannotations-api:4.6.0'
    implementation 'org.androidannotations:ormlite-api:4.6.0'
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    implementation 'com.j256.ormlite:ormlite-android:4.48'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.nispok:snackbar:2.11.0'
    implementation 'com.github.aegnor:rencode-java:cb628e824e'
    implementation('com.github.afollestad.material-dialogs:core:0.9.6.0@aar') {
        transitive = true
    }
    implementation 'com.evernote:android-job:1.2.6'
    annotationProcessor 'org.androidannotations:androidannotations:4.6.0'
    annotationProcessor 'org.androidannotations:ormlite:4.6.0'
    implementation 'com.squareup.okhttp3:okhttp:4.6.0'
    implementation "com.squareup.okhttp3:okhttp-tls:4.6.0"
    implementation 'com.android.support:multidex:1.0.3'
}

